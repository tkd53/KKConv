#!/bin/csh -f
#=====================================================================================
#                       WithTask.csh
#                           by Shinsuke Mori
#                           Last change : 15 August 2009
#=====================================================================================

# 機  能 : 疑似確率的単語分割による仮名漢字変換の実験用スクリプト
#
# 使用法 : WithTask.csh (STEP) (MULT) (TEXTSTEM)
#
# 実  例 : WithTask.csh 0 8 ../../MMH/L.text
#
# 注意点 : なし


#-------------------------------------------------------------------------------------
#                        check arguments
#-------------------------------------------------------------------------------------

if ($#argv != 3) then
    echo "Usage: $0 (STEP) (MULT) (TASK)"
    exit(1)
endif


#-------------------------------------------------------------------------------------
#                        setvariables
#-------------------------------------------------------------------------------------

set STEP = $argv[1]                               # ベースの言語モデルのディレクトリ
set MULT = $argv[2]                               # 疑似確率的単語分割コーパスの倍率
set STEM = $argv[3]

set DIRE = ${MULT}PSSC-Step$STEP


#-------------------------------------------------------------------------------------
#                        Directory の作成
#-------------------------------------------------------------------------------------

/bin/mkdir -p $DIRE

echo cd $DIRE

cd $DIRE

set STEM = ../$STEM


#-------------------------------------------------------------------------------------
#                        言語モデルへのリンク
#-------------------------------------------------------------------------------------

set BASE = ../Step$STEP

foreach FILE ( $BASE/Char* $BASE/Word* )
    /bin/ln -s "$FILE"
end


#-------------------------------------------------------------------------------------
#                        疑似確率的単語分割コーパス
#-------------------------------------------------------------------------------------

set TOOL = $HOME/link/resource/tool

set PROG = $TOOL/StochSegCorpus/bin/MonteCarlo-SSC.perl

/bin/mkdir -p corpus
foreach M ( `seq -w 1 $MULT` )
    echo $M
    $PROG $STEM | $HOME/usr/bin/split.perl corpus/$M-%02d.word 9
end

foreach K (`seq -f%02g 1 9`)
    echo $K
    cat corpus/*-$K.word > corpus/$K.word
end


#-------------------------------------------------------------------------------------
#                        無限語彙の仮名漢字変換へのリンク
#-------------------------------------------------------------------------------------

#kokokara
../bin/CrossEntropyWithTask.perl 0 corpus >& CEwT.log &


#-------------------------------------------------------------------------------------
#                        close
#-------------------------------------------------------------------------------------

echo Done $0
exit


#=====================================================================================
#                        END
#=====================================================================================
